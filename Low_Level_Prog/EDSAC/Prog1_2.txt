T 56 K
G K [директива IO2, фиксация начального адреса программы ]
[0.]A 3 F [пролог: формирование кода инструкции возврата в Acc]
[1.]T 25 @ [пролог: запись инструкции возврата]
[2.]X 0 F [использовалось для отладки]
[3.]A 0 F [загрузка в аккумулятор адреса 0-го элемента массива]
[4.]L 0 D [сдвиг на 1 разряд влево]
[5.]A 15 @ [прибавляем код инструкции с нулевым полем адреса]
[6.]T 15 @ [запись сформированной инструкции, обнуление аккумулятора]
[7.loop]A 1 F [загружаем счетчик необработанных элементов массива]
[8.]S 27 @ [вычитаем 1]
[9.]G 22 @ [если результат меньше 0, завершаем работу подпрограммы]
[10.]T 1 F [обновляем значение счетчика и обнуляем аккумулятор]
[11.]H 2 F [загружаем в мультиплексор значение x0]
[12.]V 26 @ [умножаем число в мультиплексоре на текущее b и прибавляем результат к аккумулятору]
[13.]L 64 F [сдвигаем аккумулятор на 8 разрядов влево]
[14.]L 64 F [ещё на 8, так как при умножении число сдвигается на 16, а сразу сдвинуть на 16 нельзя]
[15.r1:]A 0 F [прибавляем к аккумулятору значения из ячейки N]
[16.]T 26 @ [записываем результат в b]
[17.]A 27 @ [загрузка в аккумулятор константы 1]
[18.]L 0 D [сдвиг на 1 разряд влево]
[19.]A 15 @ [прибавляем код инструкции, исполненной на предыдущем шаге]
[20.]T 15 @ [записываем сформированную инструкцию в память]
[21.]E 7 @ [повторяем операции, начиная с 7-ой; аккумулятор обнулен]
[22.]T 0 F [обнуляем аккумулятор]
[23.]A 26 @ [запись результата в аккумулятор]
[24.]T 5 F [сохранение результата в 5-ой ячейке памяти]
[25.]E 0 F [эпилог: инструкция возврата из подпрограммы]
[26.b:]P 0 F [0]
[27.const1:]P 0 D [1]
 G K [директива IO2, фиксация начального адреса программы]
[ 0:] X 0 F [использовалось для отладки]
[ 1:] A 11 [<addr>] @ [адрес массива (в начале работы обнуление аккумулятора не требуется)]
[ 2:] R 0 D [сдвиг на 1 разряд вправо]
[ 3:] T 0 F [запись адреса массива в ячейку 0, обнуление аккумулятора]
[ 4:] A 12 [<len>] @ [длинна массива]
[ 5:] T 1 F [запись длины массива в ячейку 1, обнуление аккумулятора]
[ 6:] A 13 [<x0>] @ [X, в котором необходимо посчитать многочлен]
[ 7:] T 2 F [запись x0 в ячейку 2, обнуление аккумулятора]
[ 8:] A 8 @          [\ вызов]
[ 9:] G 56 [<sub>] F [/ подпрограммы]
[ 10:] Z 0 F [точка останова]
[addr:]
[ 11:] P 14 [<array>] @ [адрес первого элемента массива]
[len:]
[ 12:]P 2 D [длина массива]
[x0:]
[ 13:]P 1 F [точка x0, в которой необходимо почтитать многочлен]
[array:]
[14.]P 0 D [1]
[15.]P 2 D [5]
[16.]P 12 F [24]
[17.]P 0 D [1]
[18.]P 65535 D [-1]
EZ PF [директива IO2, переход к исполнению]


