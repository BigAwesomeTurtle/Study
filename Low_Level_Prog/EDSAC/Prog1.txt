     T 67 S
[32.]T 0 S [ запись аккумулятора в рабочую ячейку 0, обнуление аккумулятора]
[33.]A 58 S [загрузка в аккумулятор длины массива коэфициентов]
[34.]T 1 S [запись этого значение в ячейку 1]
[35.]A 61 S [загрузка в аккумулятор адреса 0-го элемента массива]
[36.]L 0 L  [сдвиг аккумулятора на 1 разряд влево]
[37.]A 50 S [прибавляем код инструкции с нулевым полем адреса]
[38.]T 50 S [запись сформированной инструкции, обнуление аккумулятора]
[39.loop]A 1 S [загружаем в аккумулятор счётчик необраюотанных элементов массива]
[40.]S 60 S [вычитаем 1]
[41.]E 45 S [если результат меньше нуля, записываем ответ в аккумулятор и завершаем работу]
[42.]T 0 S [обнуляем аккумулятор]
[43.]A 59 S [запись результата в аккумулятор]
[44.]Z 0 S [точка останова, завершение программы]
[45.]T 1 S [обновляем значение счетчика и обнуляем аккумулятор]
[46.]H 57 S [загружаем в мультиплексор значение x0]
[47.]V 59 S [умножаем число в мультиплексоре на текущее b и прибавляем результат к аккумулятору]
[48.]L 64 S [сдвигаем аккумулятор на 8 разрядов влево]
[49.]L 64 S [ещё на 8, так как при умножении число сдвигается на 16, а сразу сдвинуть на 16 нельзя]
[50.r1:] A 0 S [прибавляем к аккумулятору значения из ячейки N]
[51.]T 59 S [записываем результат в b]
[52.]A 60 S [загрузка в аккумулятор константы 1]
[53.]L 0 L [сдвиг на 1 разряд влево]
[54.]A 50 S [прибавляем код инструкции, исполненной на предыдущем шаге]
[55.]T 50 S [записываем сформированную инструкцию в память]
[56.]E 39 S [повторяем операции, начиная с 39-ой; аккумулятор обнулен]
[57.x0:]P 1 S [2]
[58.len:]P 2 L [5]
[59.b:]P 0 S [0]
[60.const1:]P 0 L [1]
[61.addr]P 31 S [62]
[62.]P 0 L [1]
[63.]P 2 L [5]
[64.]P 12 S [24]
[65.]P 0 L [1]
[66.]P 65535 L [-1]


